start: "spec:" _NEWLINE _INDENT _docs (_stmt | domain_stmt)* _DEDENT

domain_stmt: ("domain" arguments ":" | BLANK_DOMAIN_STMT) (inline_test | multiline_test)
BLANK_DOMAIN_STMT: /domain\s*:/
inline_test: _fun_call
multiline_test: _NEWLINE _INDENT _docs _stmt* _fun_call _DEDENT

_stmt: (mock_stmt | import_stmt) _NEWLINE

mock_stmt: "mock" _mock_path "as" test
_mock_path: mock_current_module | mock_path_absolute
mock_current_module: "." dotted_name
mock_path_absolute: dotted_name

_fun_call: results_stmt | result_by_function_stmt | throws_stmt
results_stmt: "results" [testlist] [_NEWLINE]
result_by_function_stmt: "result" [testlist] [_NEWLINE]
throws_stmt: "throws" test [_NEWLINE]

_docs: [title] [description]
?title: "title:" _inline_or_multiline_text
?description: "description:" _inline_or_multiline_text
_inline_or_multiline_text: RAW_TEXT _NEWLINE*
RAW_TEXT: /.+?(?:$|\n)/

%import .python.testlist
%import .python.arguments
%import .python.import_stmt
%import .python.dotted_name
%import .python.test
%import .python.COMMENT
%import .python._INDENT
%import .python._DEDENT
%import .python._NEWLINE
%import common.WS_INLINE
%ignore WS_INLINE
%ignore COMMENT
